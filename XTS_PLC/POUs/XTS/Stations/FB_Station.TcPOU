<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_Station" Id="{044dcdb0-796d-4bda-a3b7-df20cd71aff3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Station EXTENDS FB_TcIoXtsInfoServerInfoStation
VAR
	ax						: REFERENCE TO ARRAY [1..Tc3_XTS_Utility.TcIoXtsEnvironmentParameterList.MaxXtsMoversPerXpu] OF FB_Mover;	//Movers
	stCfg 					: REFERENCE TO stStationCfg;
	udiNumMovers			: UDINT;		//Number of Movers into the system
	lrPartLength			: LREAL;
	bInvertPolarity			: BOOL;
	INextStation			: REFERENCE TO FB_Station;//I_Station;
END_VAR
VAR_OUTPUT
	udiNumMoversIn			: UDINT;		//Number of Movers into station
	udiErrorID				: UDINT;
END_VAR
VAR
	fbMoversFIFO			: FB_MoversFIFO;	
	bWaitingMovers			: BOOL;
	udiSequenceState		: UDINT;
	sMoversWaiting			: STRING;
	sMoversIn				: STRING;
	lrActualWaitingTime		: LREAL;
	lrActualProcessTime		: LREAL;
END_VAR
VAR
	bStart  				: BOOL;
	bStop					: BOOL;
	stSecuenceStatus		: stFeedback;
	bInternalControl		: bool;
END_VAR
VAR CONSTANT
	START_POSITION_ON_PART_STATION 	: DWORD := 16#03080272;
	END_POSITION_ON_PART_STATION 	: DWORD := 16#03080273;
	STOP_POSITIONS_STATION 			: DWORD := 16#0308028D;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
]]></ST>
    </Implementation>
    <Property Name="Cfg" Id="{df3bafe3-40fc-455e-ba96-f73e28e62b7b}">
      <Declaration><![CDATA[PROPERTY Cfg : reference to stStationCfg]]></Declaration>
      <Get Name="Get" Id="{c33c444b-7999-4930-ac48-bea88e0e077f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Cfg REF= stCfg;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="configure" Id="{46534150-5d55-4e0e-8e8b-7c58883b1f5a}">
      <Declaration><![CDATA[METHOD configure : BOOL
VAR_INPUT
	m_stCfg 				: REFERENCE TO stStationCfg;
	m_udiNumMovers			: UDINT;		//Number of Movers into the system
	m_lrPartLength			: LREAL;
	m_bInvertPolarity		: BOOL;
	m_INextStation			: REFERENCE TO FB_Station;//I_Station;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stCfg 			REF= m_stCfg 			;   
udiNumMovers	:= m_udiNumMovers		;  
lrPartLength	:= m_lrPartLength		;  
bInvertPolarity	:= m_bInvertPolarity	;
INextStation	ref= m_INextStation		;  ]]></ST>
      </Implementation>
    </Method>
    <Method Name="cyclic" Id="{6acdb9ce-8873-4be1-90bf-f30e7e9c96cb}">
      <Declaration><![CDATA[METHOD cyclic : BOOL
VAR_INPUT
	m_ax	: REFERENCE TO ARRAY [1..Tc3_XTS_Utility.TcIoXtsEnvironmentParameterList.MaxXtsMoversPerXpu] OF FB_Mover;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ax REF= m_ax;
fbMoversFIFO(ax := ax);	
stSecuenceStatus := MultipleStopSecuence(bStart, bStop);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="MultipleStopSecuence" Id="{8bfcf8e7-89a8-4fff-8c75-8ea0c8fb227f}">
      <Declaration><![CDATA[METHOD PRIVATE MultipleStopSecuence : REFERENCE TO stFeedback
VAR_INPUT
	bEnable 		: BOOL;
	bAbort			: BOOL;
END_VAR
VAR_INST
	udiState  			: UDINT;
	stCurrentCfg		: stStationCfg;
	fbTON				: ARRAY [1..5] OF TON;
	i					: UDINT;
	bAllDOne			: BOOL;
	m_bBusy				: BOOL;
	aMoversIN			: ARRAY [1..3] OF UDINT;
	ErrorID				: UDINT;
	tmInitProcessTime 	: TIME;
	tmInitWaitTime		: TIME;
	stStatus			: stFeedback;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[(* 
	0. External Method to Add Movers into station
	1. Check if there is some mover in FIFO
	2. Send Mover to Stop Position
	3. Check Movers all StandStill and Reset Mover Commands
	4. Processing
	5. Send Movers to next FIFO Station
*)
IF bAbort AND udiState <> 100 THEN udiState := 100; END_IF

CASE udiState OF
	0 : //Wait for enable command
		IF bEnable THEN 
			udiNumMoversIn := 0;
			udiState := udiState + 10; 
		END_IF 
		
	10 : //Clear commands
		fbMoversFIFO.m_Clear();
		FOR i:=1 TO udiNumMovers DO
			//
			ax[i].clearCmd();
		END_FOR
		FOR i:= 1 TO 3 DO
			fbTon[i](IN := FALSE);
		END_FOR
		udiState := 19;
		
	19 : //Update configuration parameters
		stCurrentCfg 						:= stCfg;
		fbMoversFIFO.lrFirstStopPosition 	:= stCurrentCfg.lrStartPosition + stCurrentCfg.lrStopPosition[1];
		fbMoversFIFO.lrPartLength 			:= lrPartLength;
		tmInitWaitTime := TIME();
		udiState := udiState + 1;
		
	20 : //Check for new movers into FIFO
		m_bBusy 		:= TRUE;
		bWaitingMovers 	:= TRUE;
		IF fbMoversFIFO.nMovers > 0 AND udiNumMoversIn < stCurrentCfg.uiNumStopPositions THEN
			udiNumMoversIn 				:= udiNumMoversIn + 1;
			IF fbMoversFIFO.m_Extract(m_udiMoverID => aMoversIN[udiNumMoversIn]) THEN
				ax[aMoversIN[udiNumMoversIn]].clearCmd();
				bWaitingMovers := FALSE;
				udiState := udiState + 1;
			END_IF
		END_IF
		IF bEnable = FALSE THEN
			udiState := 100;
		END_IF
		
	21 : //Send Mover to Stop Position
		bAllDone := TRUE;
		FOR i:=1 TO udiNumMoversIn DO
			bAllDone := bAllDone AND ax[aMoversIN[i]].moveca_bytime(TRUE, stCurrentCfg.lrStartPosition + stCurrentCfg.lrStopPosition[stCurrentCfg.uiNumStopPositions - i + 1], stCfg.lrTravelTime).bDone;
			IF ax[aMoversIN[i]].moveca_bytime(TRUE, stCurrentCfg.lrStartPosition + stCurrentCfg.lrStopPosition[stCurrentCfg.uiNumStopPositions - i + 1], stCfg.lrTravelTime).bError THEN
				udiState := 999;
				EXIT;
			END_IF 
		END_FOR
		//If Station full process
		IF udiNumMoversIn = stCurrentCfg.uiNumStopPositions THEN
			IF bAllDone THEN 
				lrActualWaitingTime := (TIME_TO_LREAL(TIME() - tmInitWaitTime)-10.0)/1000.0;
				udiState := 30; 
			END_IF
		ELSE
			udiState := udiState - 1;
		END_IF
		
	30 : //Check if all movers are standstill and reset commands
		bAllDone := TRUE;
		FOR i:= 1 TO udiNumMoversIn DO	
			bAllDone := bAllDone AND ax[aMoversIN[i]].standstill();
			ax[aMoversIN[i]].clearCmd();
		END_FOR
		IF bAllDone THEN
			tmInitProcessTime := TIME();
			udiState := udiState + 10;
		END_IF
		
	40 : //Processing
		bAllDone := TRUE;
		FOR i:= 1 TO stCurrentCfg.uiNumStopPositions DO
			fbTon[i](IN := TRUE, PT := LREAL_TO_TIME(stCfg.lrProcessingTime*1000));
			bAllDone := bAllDone AND fbTON[i].Q;
		END_FOR
		IF (bAllDone AND stCfg.bEnableManualAck = FALSE) OR (stCfg.bEnableManualAck = TRUE AND stCfg.bManualAck) THEN
			FOR i:= 1 TO stCurrentCfg.uiNumStopPositions DO
				ax[aMoversIN[i]].clearCmd();
				fbTon[i](IN := FALSE);
			END_FOR
			lrActualProcessTime := (TIME_TO_LREAL(TIME() - tmInitProcessTime)-10.0)/1000.0;
			udiState := udiState + 10;
		END_IF
		
	50 : //Send out movers
		FOR i:= 1 TO udiNumMoversIn DO
		 	INextStation.RegisterMover(aMoversIN[i]);
			ax[aMoversIN[i]].moveca_bytime(TRUE, lrPosition := INextStation.Cfg.lrStartPosition, INextStation.Cfg.lrTravelTime);
		END_FOR
		udiNumMoversIn	:= 0;
		udiState 		:= 19;
		IF ErrorID <> 0 THEN
			udiState := 999;
		END_IF
		
	100 : //Abort
		m_bBusy := FALSE;
		bWaitingMovers := FALSE;
		IF bEnable = FALSE THEN
			ErrorID := 0;
			fbMoversFIFO.m_Clear();
			udiState := 0;
		END_IF
		
	999 : //Error
		m_bBusy := FALSE;
		bWaitingMovers := FALSE;
		IF bEnable = FALSE THEN
			fbMoversFIFO.m_Clear();
			ErrorID := 0;
			udiState := 0;
		END_IF
		
END_CASE
udiSequenceState 			:= udiState;
udiErrorID					:= ErrorID;
stStatus.udiErrorID			:= udiErrorID;
stStatus.bBusy				:= m_bBusy;
stStatus.bError				:= stStatus.udiErrorID <> 0;

MultipleStopSecuence		REF= stStatus;

sMoversWaiting := '';
FOR i:= 1 TO fbMoversFIFO.nMovers DO
	sMoversWaiting := CONCAT(sMoversWaiting, UDINT_TO_STRING(fbMoversFIFO.aMoversFIFO[i]));
	IF i < fbMoversFIFO.nMovers THEN sMoversWaiting := CONCAT(sMoversWaiting, ', '); END_IF
END_FOR
sMoversIn := '';
FOR i:= 1 TO udiNumMoversIn DO
	sMoversIn := CONCAT(sMoversIn, UDINT_TO_STRING(aMoversIN[i]));
	IF i < udiNumMoversIn THEN sMoversIn := concat(sMoversIn, ', '); END_IF
END_FOR

]]></ST>
      </Implementation>
    </Method>
    <Method Name="RegisterMover" Id="{311f5327-14aa-4b71-8341-6aaa6922ac74}">
      <Declaration><![CDATA[METHOD RegisterMover : BOOL
VAR_INPUT
	udiMoverID : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbMoversFIFO.m_Add(udiMoverID);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetStationInfo" Id="{864b431a-7d13-4073-a027-d3c853e0124b}">
      <Declaration><![CDATA[METHOD SetStationInfo : BOOL
VAR_INPUT
	bExecute		: BOOL;
	OTCID			: DWORD;
	bDisable		: BOOL;
END_VAR
VAR_INST
	iState 	 		: INT;
	fbADSWrite 		: adswrite;
	lrStartPos		: LREAL;
	lrEndPos		: LREAL;
	aStopPos		: ARRAY [1..3] OF LREAL;
	lrTmp			: LREAL;
	aTmp			: ARRAY [1..3] OF LREAL;
	lrDistance		: LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
CASE iState OF
	0 : 
		IF bExecute THEN
			fbADSWrite(WRITE:= FALSE);
			fbMoversFIFO(ax := ax);	
			iState 	:= iState + 1;
		END_IF
	
	1 : //Write Init position
		lrStartPos 	:= SEL(bInvertPolarity, stCfg.lrStartPosition, lrPartLength - stCfg.lrEndPosition);
		lrEndPos 	:= SEL(bInvertPolarity, stCfg.lrEndPosition, lrPartLength - stCfg.lrStartPosition);
		IF bDisable THEN
			lrStartPos := lrEndPos := 0;
		END_IF
		fbADSWrite(
			NETID:= , 
			PORT:= 350, 
			IDXGRP:= OTCID, 
			IDXOFFS:= START_POSITION_ON_PART_STATION, 
			LEN:= SIZEOF(lrStartPos), 
			SRCADDR:= ADR(lrStartPos), 
			WRITE:= TRUE, 
			TMOUT:= , 
			BUSY=> , 
			ERR=> , 
			ERRID=> );
	
		IF fbADSWrite.BUSY = FALSE AND fbADSWrite.ERR = FALSE THEN
			fbADSWrite(WRITE := FALSE);
			iState := iState + 1;
		END_IF	
			
	2 : //Write End Position
		fbADSWrite(
			NETID:= , 
			PORT:= 350, 
			IDXGRP:= OTCID, 
			IDXOFFS:= END_POSITION_ON_PART_STATION, 
			LEN:= SIZEOF(lrEndPos), 
			SRCADDR:= ADR(lrEndPos), 
			WRITE:= TRUE, 
			TMOUT:= , 
			BUSY=> , 
			ERR=> , 
			ERRID=> );
	
		IF fbADSWrite.BUSY = FALSE AND fbADSWrite.ERR = FALSE THEN
			fbADSWrite(WRITE := FALSE);
			iState := iState + 1;
		END_IF
		
	3 : //Write Stop Positions
		aStopPos[1] := SEL(bInvertPolarity, stCfg.lrStopPosition[1], (stCfg.lrEndPosition - stCfg.lrStartPosition) - stCfg.lrStopPosition[1]); 
		aStopPos[2] := SEL(bInvertPolarity, stCfg.lrStopPosition[2], (stCfg.lrEndPosition - stCfg.lrStartPosition) - stCfg.lrStopPosition[2]); 
		aStopPos[3] := SEL(bInvertPolarity, stCfg.lrStopPosition[3], (stCfg.lrEndPosition - stCfg.lrStartPosition) - stCfg.lrStopPosition[3]); 
		IF stCfg.uiNumStopPositions = 1 AND NOT bDisable THEN
			aTmp[1] := aStopPos[1];
			aTmp[2] := aStopPos[1];
			aTmp[3] := aStopPos[1];
		ELSIF stCfg.uiNumStopPositions = 2 AND NOT bDisable THEN
			aTmp[1] := SEL(bInvertPolarity, aStopPos[1], aStopPos[2]);
			aTmp[2] := SEL(bInvertPolarity, aStopPos[2], aStopPos[1]);
			aTmp[3] := SEL(bInvertPolarity, aStopPos[2], aStopPos[1]);
		ELSIF stCfg.uiNumStopPositions = 3 AND NOT bDisable THEN
			aTmp[1] := SEL(bInvertPolarity, aStopPos[1], aStopPos[3]);
			aTmp[2] := SEL(bInvertPolarity, aStopPos[2], aStopPos[2]);
			aTmp[3] := SEL(bInvertPolarity, aStopPos[3], aStopPos[1]);
		ELSE
			aTmp[1]	:= 0.0;
			aTmp[2] := 0.0;
			aTmp[3] := 0.0;
		END_IF
		fbADSWrite(
			NETID:= , 
			PORT:= 350, 
			IDXGRP:= OTCID, 
			IDXOFFS:= STOP_POSITIONS_STATION, 
			LEN:= SIZEOF(aTmp), 
			SRCADDR:= ADR(aTmp), 
			WRITE:= TRUE, 
			TMOUT:= , 
			BUSY=> , 
			ERR=> , 
			ERRID=> );
	
		IF fbADSWrite.BUSY = FALSE AND fbADSWrite.ERR = FALSE THEN
			fbADSWrite(WRITE := FALSE);
			iState := iState + 1;
		END_IF
		
	4 : //Job Done
		SetStationInfo := TRUE;
		IF bExecute = FALSE THEN
			iState := 0;
		END_IF
	
END_CASE

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{7e3fcd60-c486-4296-9773-b326a33bfd56}">
      <Declaration><![CDATA[METHOD Start : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bStart := TRUE;
bStop  := FALSE;
Start  := stSecuenceStatus.bBusy;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Stop" Id="{773df169-21c9-4e2d-ad2a-e19100b56fc5}">
      <Declaration><![CDATA[METHOD Stop : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bStart := FALSE;
bStop  := TRUE;
Stop   := NOT stSecuenceStatus.bBusy;

]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>